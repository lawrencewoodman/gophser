package require tcltest
namespace import tcltest::*
set ThisScriptDir [file dirname [info script]]
set RepoRootDir [file join $ThisScriptDir ..]

source [file join $ThisScriptDir test_helpers.tcl]
source [file join $RepoRootDir router.tcl]


# Create the routes to test against
gophers::router::route "/files" testFiles
gophers::router::route "/files/*" testFilesSplat
gophers::router::route "/" testRoot
gophers::router::route "/*/*" testDirFilename


test getHandler-1 {Return false if route not found} \
-setup {
  set selector "bob"
} -body {
  gophers::router::getHandler $selector
} -result {}


test getHandler-2 {Detect an empty pattern for the root} \
-setup {
  set selector ""
} -body {
  gophers::router::getHandler $selector
} -result testRoot


test getHandler-3 {Detect an empty pattern for the root with a trailing slash} \
-setup {
  set selector {/}
} -body {
  gophers::router::getHandler $selector
} -result testRoot


test getHandler-4 {Detect a single splat on its own} \
-setup {
  set selector {/files/fred.txt}
} -body {
  gophers::router::getHandler $selector
} -result testFilesSplat


test getHandler-5 {Detect a single splat on its own against multiple sub-directories} \
-setup {
  set selector {/files/something/bob.txt}
} -body {
  gophers::router::getHandler $selector
} -result testFilesSplat


test getHandler-6 {Detect named parameters} \
-setup {
  set selector {/dirA/someFile.txt}
} -body {
  gophers::router::getHandler $selector
} -result testDirFilename


test getHandler-7 {Detect exact path} \
-setup {
  set selector {/files}
} -body {
  gophers::router::getHandler $selector
} -result testFiles


test getHandler-8 {Detect exact path with trailing slash} \
-setup {
  set selector {/files/}
} -body {
  gophers::router::getHandler $selector
} -result testFiles


test safeSelector-1 {} \
-setup {
  set selectors {
    /tests
    /fred/bob/dave
    /fred/bob/..
    /fred/bob/../..
    /./gerald/.
    /~fred
    /.fred
    {}
    /
  }
} -body {
  set res {}
  foreach selector $selectors {
    lappend res [gophers::router::safeSelector $selector]
  }
  set res
} -result [list /tests /fred/bob/dave /fred / /gerald /~fred \
                /.fred / /]

cleanupTests
