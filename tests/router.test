package require tcltest
namespace import tcltest::*
set ThisScriptDir [file dirname [info script]]
set RepoRootDir [file join $ThisScriptDir ..]

source [file join $ThisScriptDir test_helpers.tcl]
source [file join $RepoRootDir router.tcl]

# Create the routes to test against
router::route "" testRoot
router::route "/files" testFiles
router::route "/files/*" testFilesSplat
router::route "/{dir}/{filename}" testDirFilename


test getHandlerInfo-1 {Return false if route not found} \
-setup {
  set selector "bob"
} -body {
  router::getHandlerInfo $selector
} -result {}


test getHandlerInfo-2 {Detect an empty pattern for the root} \
-setup {
  set selector ""
} -body {
  router::getHandlerInfo $selector
} -result [list testRoot {/}]


test getHandlerInfo-3 {Detect an empty pattern for the root with a trailing slash} \
-setup {
  set selector {/}
} -body {
  router::getHandlerInfo $selector
} -result [list testRoot {/}]


test getHandlerInfo-4 {Detect a single splat on its own} \
-setup {
  set selector {/files/fred.txt}
} -body {
  router::getHandlerInfo $selector
} -result [list testFilesSplat {/files/fred.txt fred.txt}]


test getHandlerInfo-5 {Detect a single splat on its own against multiple sub-directories} \
-setup {
  set selector {/files/something/bob.txt}
} -body {
  router::getHandlerInfo $selector
} -result [list testFilesSplat {/files/something/bob.txt something/bob.txt}]


test getHandlerInfo-6 {Detect named parameters} \
-setup {
  set selector {/dirA/someFile.txt}
} -body {
  router::getHandlerInfo $selector
} -result [list testDirFilename {/dirA/someFile.txt dirA someFile.txt}]


test getHandlerInfo-7 {Detect exact path} \
-setup {
  set selector {/files}
} -body {
  router::getHandlerInfo $selector
} -result [list testFiles  {/files}]


test getHandlerInfo-8 {Detect exact path with trailing slash} \
-setup {
  set selector {/files/}
} -body {
  router::getHandlerInfo $selector
} -result [list testFiles {/files}]


test safeSelector-1 {} \
-setup {
  set selectors {
    /tests
    /fred/bob/dave
    /fred/bob/..
    /fred/bob/../..
    /./gerald/.
    /~fred
    /.fred
    {}
  }
} -body {
  set res {}
  foreach selector $selectors {
    lappend res [router::safeSelector $selector]
  }
  set res
} -result [list /tests /fred/bob/dave /fred / /gerald /~fred \
                /.fred /]

cleanupTests
