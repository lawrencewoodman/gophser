package require tcltest
namespace import tcltest::*

apply {{} {
  set thisScriptDir [file dirname [info script]]
  set moduleDir [file normalize [file join $thisScriptDir ..]]
  set modules [lsort -decreasing [glob -directory $moduleDir gophser-*.tm]]
  source [lindex $modules 0]
  source [file join $thisScriptDir test_helpers.tcl]
  TestHelpers::setRepoRootDir $moduleDir
}}


# Create the routes to test against
gophser::router::route "/files" testFiles
gophser::router::route "/files/*" testFilesSplat
gophser::router::route "/" testRoot
gophser::router::route "/*/*" testDirFilename
gophser::router::route "URL:*" testServeURL


test getHandler-1 {Return false if route not found} \
-setup {
  set selector "bob"
} -body {
  gophser::router::getHandler $selector
} -result {}


test getHandler-2 {Detect an empty pattern for the root} \
-setup {
  set selector ""
} -body {
  gophser::router::getHandler $selector
} -result testRoot


test getHandler-3 {Detect an empty pattern for the root with a trailing slash} \
-setup {
  set selector {/}
} -body {
  gophser::router::getHandler $selector
} -result testRoot


test getHandler-4 {Detect a single splat on its own} \
-setup {
  set selector {/files/fred.txt}
} -body {
  gophser::router::getHandler $selector
} -result testFilesSplat


test getHandler-5 {Detect a single splat on its own against multiple sub-directories} \
-setup {
  set selector {/files/something/bob.txt}
} -body {
  gophser::router::getHandler $selector
} -result testFilesSplat


test getHandler-6 {Detect named parameters} \
-setup {
  set selector {/dirA/someFile.txt}
} -body {
  gophser::router::getHandler $selector
} -result testDirFilename


test getHandler-7 {Detect exact path} \
-setup {
  set selector {/files}
} -body {
  gophser::router::getHandler $selector
} -result testFiles


test getHandler-8 {Detect exact path with trailing slash} \
-setup {
  set selector {/files/}
} -body {
  gophser::router::getHandler $selector
} -result testFiles


test getHandler-8 {Check non standard path used with URL: prefix} \
-setup {
  set selectors {
    {URL:https://example.com/blog}
    {URL: https://example.com/blog}
  }
} -body {
  lmap selector $selectors {
    gophser::router::getHandler $selector
  }
} -result {testServeURL testServeURL}


test safeSelector-1 {} \
-setup {
  set selectors {
    {URL:https://example.com/blog}
    {URL: https://example.com/blog}
    /tests
    /fred/bob/dave
    /fred/bob/..
    /fred/bob/../..
    /./gerald/.
    /~fred
    /.fred
    {}
    /
  }
} -body {
  lmap selector $selectors {
    gophser::router::safeSelector $selector
  }
} -result [list {URL:https://example.com/blog} \
                {URL: https://example.com/blog} \
                /tests /fred/bob/dave /fred / /gerald /~fred \
                /.fred / /]


cleanupTests
